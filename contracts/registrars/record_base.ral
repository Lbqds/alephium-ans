Abstract Contract RecordBase(
  registrar: ByteVec,
  mut owner: Address,
  mut ttl: U256,
  refundAddress: Address
) extends Constants() implements Record {
  pub fn getOwner() -> Address {
    return owner
  }

  @using(updateFields = true)
  pub fn setOwner(newOwner: Address) -> () {
    checkCaller!(callerAddress!() == owner, ErrorCodes.InvalidCaller)
    owner = newOwner
  }

  pub fn getRefundAddress() -> Address {
    return refundAddress
  }

  pub fn getTTL() -> U256 {
    return ttl
  }

  @using(updateFields = true)
  pub fn setTTL(newTTL: U256) -> () {
    checkCaller!(callerContractId!() == registrar, ErrorCodes.InvalidCaller)
    ttl = newTTL
  }

  @using(assetsInContract = true)
  pub fn destroy() -> () {
    checkCaller!(callerContractId!() == registrar, ErrorCodes.InvalidCaller)
    destroySelf!(refundAddress)
  }
}