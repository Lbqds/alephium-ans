Contract SecondaryRegistrar(primaryRegistrar: PrimaryRegistrar, recordTemplateId: ByteVec) extends Constants() {
  @using(preapprovedAssets = true, checkExternalCaller = false)
  pub fn register(
    name: ByteVec,
    owner: Address,
    payer: Address,
    credentialTokenId: ByteVec,
    ttl: U256
  ) -> () {
    assert!(size!(name) != 0, ErrorCodes.InvalidArgs)
    assert!(isAssetAddress!(payer), ErrorCodes.ExpectAssetAddress)
    assert!(blockTimeStamp!() < ttl, ErrorCodes.InvalidArgs)

    let node = keccak256!(name)
    let credentialTokenPath = node ++ u256To32Byte!(ttl)
    // TODO: improve this
    let primaryRegistrarGroup = byteVecSlice!(contractId!(primaryRegistrar), 31, 32)
    let expectedCredentialTokenId = byteVecSlice!(subContractIdOf!(primaryRegistrar, credentialTokenPath), 0, 31) ++ primaryRegistrarGroup
    assert!(credentialTokenId == expectedCredentialTokenId, ErrorCodes.InvalidCredentialToken)
    assert!(tokenRemaining!(payer, credentialTokenId) == 1, ErrorCodes.InvalidCredentialToken)

    let recordId = subContractId!(node)
    if (contractExists!(recordId)) {
      let record = Record(recordId)
      assert!(record.getTTL() < ttl, ErrorCodes.InvalidCredentialToken)
      record.destroy()
    }

    let (encodedImmutableFields, encodedMutableFields) = Record.encodeFields!(selfContractId!(), owner, ttl, payer)
    copyCreateSubContract!{payer -> ALPH: 1 alph}(
      node,
      recordTemplateId,
      encodedImmutableFields,
      encodedMutableFields
    )
  }
}