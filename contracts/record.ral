Contract Record(
  registrar: ByteVec,
  mut owner: Address, // The owner can be a contract address
  mut ttl: U256,
  mut resolver: Resolver,
  refundAddress: Address
) extends Constants() {
  pub fn getRegistrar() -> ByteVec {
    return registrar
  }

  pub fn getOwner() -> Address {
    return owner
  }

  @using(updateFields = true)
  pub fn setOwner(newOwner: Address) -> () {
    checkCaller!(callerContractId!() == registrar, ErrorCodes.InvalidCaller)
    owner = newOwner
  }

  pub fn getTTL() -> U256 {
    return ttl
  }

  @using(updateFields = true)
  pub fn setTTL(newTTL: U256) -> () {
    checkCaller!(callerContractId!() == registrar, ErrorCodes.InvalidCaller)
    ttl = newTTL
  }
 
  @using(updateFields = true)
  pub fn setResolver(newResolver: Resolver) -> () {
    checkCaller!(callerContractId!() == registrar, ErrorCodes.InvalidCaller)
    resolver = newResolver
  }

  pub fn getRefundAddress() -> Address {
    return refundAddress
  }

  @using(assetsInContract = true)
  pub fn destroy(node: ByteVec) -> () {
    checkCaller!(callerContractId!() == registrar, ErrorCodes.InvalidCaller)
    resolver.removeNode(node, refundAddress)
    burnToken!(selfAddress!(), selfContractId!(), 1)
    destroySelf!(refundAddress)
  }
}