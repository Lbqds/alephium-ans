Abstract Contract NameResolver(
  ansRegistryId: ByteVec,
  nameInfoTemplateId: ByteVec
) extends ResolverBase(ansRegistryId), Constants() {
  @using(preapprovedAssets = true)
  fn createNameInfo(node: ByteVec, name: ByteVec, payer: Address) -> () {
    checkCaller!(callerAddress!() == getOwner(node), ErrorCodes.InvalidCaller)
    copyCreateSubContract!{payer -> ALPH: 1 alph}(
      PathPrefix.NameInfo ++ node,
      nameInfoTemplateId,
      NameInfo.encodeImmFields!(selfContractId!()),
      NameInfo.encodeMutFields!(name)
    )
  }

  @using(preapprovedAssets = true)
  pub fn setName(node: ByteVec, name: ByteVec) -> () {
    let caller = callerAddress!()
    checkCaller!(caller == getOwner(node), ErrorCodes.InvalidCaller)
    let contractId = subContractId!(PathPrefix.NameInfo ++ node)
    if (contractExists!(contractId)) {
      let nameInfo = NameInfo(contractId)
      nameInfo.setName(name)
    } else {
      createNameInfo{caller -> ALPH: 1 alph}(node, name, caller)
    }
  }

  pub fn getName(node: ByteVec) -> ByteVec {
    let nameInfo = NameInfo(subContractId!(PathPrefix.NameInfo ++ node))
    return nameInfo.getName()
  }

  pub fn removeNameInfo(node: ByteVec, refundAddress: Address) -> () {
    checkCaller!(callerAddress!() == getOwner(node), ErrorCodes.InvalidCaller)
    let contractId = subContractId!(PathPrefix.NameInfo ++ node)
    if (contractExists!(contractId)) {
      let nameInfo = NameInfo(contractId)
      nameInfo.destroy(refundAddress)
    }
  }
}
